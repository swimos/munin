buildscript {
  repositories {
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:9.1.1'
  }
}

apply plugin: 'application'
apply plugin: 'nebula.ospackage-application'

ext.swimVersion = project.property('swim.version')
version = project.property('filethesebirds.version')

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  // Swim core
  implementation group: 'org.swimos', name: 'swim-api', version: swimVersion
  implementation group: 'org.swimos', name: 'swim-server', version: swimVersion
  implementation group: 'org.swimos', name: 'swim-store-db', version: swimVersion
  // Swim toolkit
  implementation group: 'inc.swim', name: 'swim-adapter-common', version: swimVersion
  implementation group: 'inc.swim', name: 'swim-adapter-http', version: swimVersion
  // Markdown
  implementation group: 'org.commonmark', name: 'commonmark', version: '0.19.0'
  implementation group: 'org.commonmark', name: 'commonmark-ext-autolink', version: '0.19.0'
  // String shenanigans
  implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'

  testImplementation 'org.testng:testng:7.6.1'
}

compileJava {
  options.release = 11
}

application {
  mainClass = 'filethesebirds.munin.Main'
}

test {
  useTestNG()
  scanForTestClasses = false
  include '**/*Spec.class'
  testLogging {
    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    exceptionFormat 'full'
    displayGranularity = 4
  }
}

ospackage {
  release '1'
  prefix '/opt/swim-munin'
}

task packageDeb(type: Deb) {
  maintainer = 'developer@swim.inc'

  configurationFile("/etc/sysconfig/${project.name}")
  preInstall "addgroup --quiet --system ${project.name}"
  preInstall "adduser --quiet --system --ingroup ${project.name} --no-create-home --disabled-password ${project.name}"
  postInstall "systemctl preset ${project.name} > /dev/null 2>&1"
  postInstall "systemctl start ${project.name} > /dev/null 2>&1"
  preUninstall  "systemctl disable ${project.name} > /dev/null 2>&1"
  preUninstall  "systemctl stop ${project.name} > /dev/null 2>&1"
  postUninstall "systemctl daemon-reload > /dev/null 2>&1"

  from('pkg') {
    into '/etc/systemd/system'
    include '*.service'
    addParentDirs false
    expand project.properties
    user 'root'
    permissionGroup 'root'
    fileMode = 0644
  }

  from('pkg') {
    into '/etc/sysconfig'
    include "${project.name}"
    user 'root'
    permissionGroup 'root'
    fileMode = 0644
    fileType CONFIG | NOREPLACE
  }
}
